// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {IERC721} from "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import {IERC20Metadata} from "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {IHooks} from "@uniswap/v4-core/src/interfaces/IHooks.sol";
import {IPoolManager} from "@uniswap/v4-core/src/interfaces/IPoolManager.sol";
import {Currency} from "@uniswap/v4-core/src/types/Currency.sol";
import {PoolKey} from "@uniswap/v4-core/src/types/PoolKey.sol";
import {PoolId} from "@uniswap/v4-core/src/types/PoolId.sol";
import {TickMath} from "@uniswap/v4-core/src/libraries/TickMath.sol";
import {FullMath} from "@uniswap/v4-core/src/libraries/FullMath.sol";
import {FixedPoint96} from "@uniswap/v4-core/src/libraries/FixedPoint96.sol";
import {IPositionManager} from "@uniswap/v4-periphery/src/interfaces/IPositionManager.sol";
import {ActionConstants} from "@uniswap/v4-periphery/src/libraries/ActionConstants.sol";
import {ReentrancyGuardTransient} from "solady/utils/ReentrancyGuardTransient.sol";
import {SafeCastLib} from "solady/utils/SafeCastLib.sol";
import {IStrategy} from "../interfaces/IStrategy.sol";
import {IBeneficiaryVault} from "../interfaces/IBeneficiaryVault.sol";
import {IFeeSplitter} from "../interfaces/IFeeSplitter.sol";
import {PositionPlanner} from "../libraries/PositionPlanner.sol";
import {Plan, Position, CurrencyAmounts, PositionDefinition} from "../types/PositionPlannerTypes.sol";

/// @notice The launch configuration carried in `configData`.
/// @param feeBeneficiary The recipient which will receive creator fees if enabled
struct InstantLaunchConfig {
    address feeBeneficiary;
}

/// @title QuoteInstantLaunchStrategy
/// @notice InstantLaunchStrategy variant that opens the pool against a fixed ERC20 quote
///         currency (e.g. a tokenized stock) instead of the native currency. The launched token
///         must sort above the quote currency (mine the launch salt), keeping it currency1 so the
///         original single-sided position math applies unchanged.
/// @custom:security-contact security@uniswap.org
contract QuoteInstantLaunchStrategy is IStrategy, ReentrancyGuardTransient {
    using SafeERC20 for IERC20;
    using PositionPlanner for *;

    /// @notice Total token supply required for every launch.
    uint256 public constant TOTAL_SUPPLY = 1_000_000_000e18;
    /// @notice Static LP fee of 25 bps
    uint24 public constant LP_FEE = 10_000;
    /// @notice Tick spacing, equal to the LP fee in bps
    int24 public constant TICK_SPACING = 100;
    /// @notice Lower tick of every launch position ensuring that in order to overflow maxLiquidityPerTick
    ///         at this tick from either adjacent range, an attacker would require more than the total
    ///         supply of the token which is not possible.
    int24 public constant MIN_LAUNCH_TICK = -160_100;
    /// @notice Highest initial tick, keeping saturating maxLiquidityPerTick at the launch position's
    ///         upper tick prohibitively expensive.
    int24 public constant MAX_INITIAL_TICK = 251_325;
    /// @notice Canonical burn address
    address internal constant BURN_ADDRESS = address(0xdead);

    /// @notice The LiquidityLauncher instance which can initialize distributions.
    address public immutable launcher;
    /// @notice The v4 position manager that mints the launch position.
    IPositionManager public immutable positionManager;
    /// @notice The v4 pool manager.
    IPoolManager public immutable poolManager;
    /// @notice The singleton fee splitter that permanently locks every launch position and
    ///         permissionlessly distributes its fees.
    IFeeSplitter public immutable feeSplitter;
    /// @notice The vault that registers each launch's fee beneficiary and collects their fee share.
    /// @dev Can be the zero address to opt out of creator fees.
    IBeneficiaryVault public immutable beneficiaryVault;
    /// @notice The fixed ERC20 quote currency every launch pairs against (currency0 of every pool).
    address public immutable quoteToken;
    /// @notice Tick at which the pool opens
    int24 public immutable initialTick;
    /// @notice Initial pool sqrt price derived from the initial tick.
    uint160 public immutable initialSqrtPriceX96;
    /// @notice Liquidity of the single-sided launch position holding the full supply.
    uint128 public immutable positionLiquidity;

    /// @notice Thrown when an address required by the strategy is zero.
    error ZeroAddress();
    /// @notice Thrown when a caller other than the configured launcher initializes a distribution.
    error OnlyLauncher();
    /// @notice Thrown when the launch configuration is missing.
    error InvalidConfigData();
    /// @notice Thrown when the supplied or reported token supply is not fixed.
    error InvalidSupply();
    /// @notice Thrown when the token does not use 18 decimals.
    error InvalidTokenDecimals();
    /// @notice Thrown when the quote token is invalid (no code or not 18 decimals).
    error InvalidQuoteToken();
    /// @notice Thrown when the launched token does not sort above the quote currency.
    ///         Mine the launch salt until the token address is greater than the quote token.
    error TokenNotAboveQuote(address token, address quoteToken);
    /// @notice Thrown when the configured tick cannot define the launch range.
    error InvalidTickRange();
    /// @notice Thrown when the fee splitter or beneficiary vault is not bound to the same
    ///         PositionManager as this strategy.
    /// @param mismatchedPositionManager The mismatched PositionManager
    error PositionManagerMismatch(address mismatchedPositionManager);
    /// @notice Thrown when the plan does not resolve to exactly the precomputed launch position.
    error InvalidPositions();
    /// @notice Thrown when the configured fee beneficiary is the zero address or the launcher.
    /// @param feeBeneficiary The invalid fee beneficiary
    error InvalidFeeBeneficiary(address feeBeneficiary);
    /// @notice Thrown when the amount received differs from the amount pulled (fee-on-transfer guard).
    /// @param received The amount actually received
    /// @param expected The amount expected
    error TokenAmountMismatch(uint256 received, uint256 expected);

    /// @notice Emitted when a token is launched.
    /// @param poolId The identifier of the initialized pool.
    /// @param token The launched token.
    /// @param finalPositionRecipient The permanent recipient of the launch LP position.
    /// @param key The initialized pool key.
    event TokenLaunched(
        PoolId indexed poolId, address indexed token, address indexed finalPositionRecipient, PoolKey key
    );

    constructor(
        address _launcher,
        IPositionManager _positionManager,
        IPoolManager _poolManager,
        IFeeSplitter _feeSplitter,
        IBeneficiaryVault _beneficiaryVault,
        int24 _initialTick,
        address _quoteToken
    ) {
        if (
            _launcher == address(0) || address(_positionManager) == address(0) || address(_poolManager) == address(0)
                || address(_feeSplitter) == address(0)
        ) {
            revert ZeroAddress();
        }
        // The splitter collects through its own PositionManager; a mismatch would leave every
        // launch position's fees permanently uncollectable.
        if (_feeSplitter.positionManager() != _positionManager) {
            revert PositionManagerMismatch(address(_feeSplitter.positionManager()));
        }
        // Registration proves custody against the vault's own PositionManager; a mismatch would
        // revert every launch at registration.
        if (address(_beneficiaryVault) != address(0) && _beneficiaryVault.positionManager() != _positionManager) {
            revert PositionManagerMismatch(address(_beneficiaryVault.positionManager()));
        }
        // The tick must be aligned and leave a non-empty range above the launch floor: the launch position
        // spans [MIN_LAUNCH_TICK, initialTick] on the token side of the price.
        if (_initialTick % TICK_SPACING != 0 || _initialTick > MAX_INITIAL_TICK || _initialTick <= MIN_LAUNCH_TICK) {
            revert InvalidTickRange();
        }

        // The quote must be a live 18-decimals ERC20 so the launch tick math matches the
        // native-quote instances one for one.
        if (_quoteToken == address(0) || _quoteToken.code.length == 0) revert InvalidQuoteToken();
        if (IERC20Metadata(_quoteToken).decimals() != 18) revert InvalidQuoteToken();
        quoteToken = _quoteToken;

        launcher = _launcher;
        poolManager = _poolManager;
        positionManager = _positionManager;
        feeSplitter = _feeSplitter;
        // The beneficiary vault is optional. Setting it to the zero address opts out of creator fees for all launches.
        beneficiaryVault = _beneficiaryVault;
        initialTick = _initialTick;
        initialSqrtPriceX96 = TickMath.getSqrtPriceAtTick(_initialTick);

        positionLiquidity = SafeCastLib.toUint128(
            FullMath.mulDiv(
                TOTAL_SUPPLY, FixedPoint96.Q96, initialSqrtPriceX96 - TickMath.getSqrtPriceAtTick(MIN_LAUNCH_TICK)
            )
        );
    }

    /// @inheritdoc IStrategy
    /// @param configData The abi-encoded `InstantLaunchConfig` containing the address to route creator fees to.
    /// @dev If creator fees are not enabled, the configData is not used but must be provided.
    function initializeDistribution(address token, uint256 totalSupply, bytes calldata configData, bytes32)
        external
        override
        nonReentrant
    {
        if (msg.sender != launcher) revert OnlyLauncher();
        if (configData.length == 0) revert InvalidConfigData();
        InstantLaunchConfig memory config = abi.decode(configData, (InstantLaunchConfig));
        _validateFeeBeneficiary(config.feeBeneficiary);
        // Only accept standard tokens
        if (totalSupply != TOTAL_SUPPLY || IERC20(token).totalSupply() != TOTAL_SUPPLY) revert InvalidSupply();
        if (IERC20Metadata(token).decimals() != 18) revert InvalidTokenDecimals();

        // v4 requires currency0 < currency1; the position math below also assumes the launched
        // token is currency1. The launch salt must be mined until the token sorts above the quote.
        if (token <= quoteToken) revert TokenNotAboveQuote(token, quoteToken);

        uint256 balanceBefore = _pull(token, totalSupply);

        PoolKey memory key = PoolKey({
            currency0: Currency.wrap(quoteToken),
            currency1: Currency.wrap(token),
            fee: LP_FEE,
            tickSpacing: TICK_SPACING,
            hooks: IHooks(address(0))
        });
        PoolId poolId = key.toId();

        // Will revert if the pool is already initialized.
        poolManager.initialize(key, initialSqrtPriceX96);

        Plan memory plan;
        {
            PositionDefinition[] memory definitions = new PositionDefinition[](1);
            definitions[0] = PositionDefinition({
                // The token is currency1, so its single-sided range sits below the opening price:
                // from the launch floor up to the initial tick.
                offsetLower: MIN_LAUNCH_TICK - initialTick,
                offsetUpper: 0,
                weight: PositionPlanner.MPS,
                overridePositionRecipient: address(0)
            });

            definitions.validate();
            // The position is minted to this strategy and transferred to the fee splitter below
            (Position[] memory positions,) = definitions.resolve(
                initialSqrtPriceX96, TICK_SPACING, CurrencyAmounts({amount0: 0, amount1: TOTAL_SUPPLY}), address(this)
            );
            // Require exact liquidity to be added
            if (positions.length != 1 || positions[0].liquidity != positionLiquidity) revert InvalidPositions();
            // Encode the position into a plan
            plan = positions.toPlan(key, ActionConstants.MSG_SENDER);
        }

        IERC20(token).safeTransfer(address(positionManager), TOTAL_SUPPLY);
        // Cache the next tokenId which will be minted
        uint256 tokenId = positionManager.nextTokenId();
        positionManager.modifyLiquidities(abi.encode(plan.actions, plan.params), block.timestamp);

        // Burn any dust leftover from creating the initial position
        uint256 balanceNow = IERC20(token).balanceOf(address(this));
        if (balanceNow > balanceBefore) IERC20(token).safeTransfer(BURN_ADDRESS, balanceNow - balanceBefore);

        emit DistributionInitialized(address(this), token, totalSupply);
        emit TokenLaunched(poolId, token, address(feeSplitter), key);

        // Optionally register the beneficiary of the position if creator fees are enabled.
        if (address(beneficiaryVault) != address(0)) {
            beneficiaryVault.registerBeneficiary(tokenId, config.feeBeneficiary);
        }
        // Transfer the position to the fee splitter
        IERC721(address(positionManager)).transferFrom(address(this), address(feeSplitter), tokenId);
    }

    /// @notice Validates a launch's fee beneficiary.
    /// @dev Cannot be the zero address or the liquidity launcher.
    function _validateFeeBeneficiary(address feeBeneficiary) private view {
        if (feeBeneficiary == address(0) || feeBeneficiary == launcher) {
            revert InvalidFeeBeneficiary(feeBeneficiary);
        }
    }

    /// @notice Pulls exactly `amount` of `token` from `msg.sender`
    /// @dev Reverts if the amount received was less than expected due to fee-on-transfer tokens.
    /// @param token The token to pull.
    /// @param amount The amount to pull.
    /// @return balanceBefore The strategy's token balance before the pull.
    function _pull(address token, uint256 amount) private returns (uint256 balanceBefore) {
        balanceBefore = IERC20(token).balanceOf(address(this));
        IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
        uint256 received = IERC20(token).balanceOf(address(this)) - balanceBefore;
        if (received != amount) revert TokenAmountMismatch(received, amount);
    }

    /// @notice Accept ETH
    receive() external payable {}
}
